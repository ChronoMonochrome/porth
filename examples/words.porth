include "std.porth"

macro fd mem end
macro statbuf fd 8 + end
macro content statbuf sizeof(stat) + end
macro line content sizeof(Str) + end
macro word line sizeof(Str) + end

if argc 2 < do
  "Usage: " eputs 0 nth_argv cstr-to-str eputs " <file>\n" eputs
  "ERROR: no input file is provided\n" eputs
  1 exit
end

O_RDONLY   // flags
1 nth_argv // pathname
AT_FDCWD   // dirfd
openat

if dup 0 < do
  "ERROR: could not open file " eputs 1 nth_argv cstr-to-str eputs "\n" eputs
  1 exit
end

fd !64

if statbuf fd @64 fstat 0 < do
  "ERROR: could not determine the size of file " eputs 1 nth_argv cstr-to-str eputs "\n" eputs
  1 exit
end

statbuf @stat.st_size content !Str.count

0                        // offset
fd @64                   // fd
MAP_PRIVATE              // flags
PROT_READ                // prot
content @Str.count       // length
NULL                     // addr
mmap
content !Str.data

if content @Str.data cast(int) 0 < do
  "ERROR: could not memory map file " eputs 1 nth_argv cstr-to-str eputs "\n" eputs
  1 exit
end

while content @Str.count 0 > do
  line content str-chop-line
  while line @Str.count 0 > do
     line str-trim-left
     word line str-chop-word
     "|" puts word @Str puts "|\n" puts
  end
end
